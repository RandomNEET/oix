# https://github.com/NixOS/nixpkgs/blob/nixos-unstable/pkgs/by-name/fc/fcitx5-rime/package.nix
# https://github.com/NixOS/nixpkgs/blob/nixos-unstable/pkgs/by-name/ri/rime-ice/package.nix
# https://github.com/fcitx/fcitx5-rime
# https://github.com/iDvel/rime-ice
{
  lib,
  stdenv,
  stdenvNoCC,
  fetchFromGitHub,
  fetchurl,
  pkg-config,
  cmake,
  kdePackages,
  gettext,
  zstd,
  fcitx5,
  librime,
  rime-data,
  symlinkJoin,
}:
let
  rime-ice = stdenvNoCC.mkDerivation (finalAttrs: {
    pname = "rime-ice";
    version = "2026.03.26";

    src = fetchFromGitHub {
      owner = "iDvel";
      repo = "rime-ice";
      tag = finalAttrs.version;
      hash = "sha256-hRtA1cYAQm7M+dPSThedqKogr8YMkP9WQFEZw5pdCbU=";
    };

    installPhase = ''
      runHook preInstall
      rm -rf others README.md .git*
      mv default.yaml rime_ice_suggestion.yaml

      mkdir -p $out/share/rime-data
      cp -r . $out/share/rime-data
      runHook postInstall
    '';
  });

  combinedRimeData = symlinkJoin {
    name = "rime-data-combined";
    paths = [
      rime-data
      rime-ice
    ];
    postBuild = ''
      mkdir -p $out/share/rime-data
      [ -e "$out/share/rime-data/default.yaml" ] || touch "$out/share/rime-data/default.yaml"
    '';
  };
in
stdenv.mkDerivation rec {
  pname = "fcitx5-rime";
  version = "5.1.13";

  src = fetchurl {
    url = "https://download.fcitx-im.org/fcitx5/${pname}/${pname}-${version}.tar.zst";
    hash = "sha256-KB4IOLq6mRB1ZnOPg4Avpk6vp29xbLkDGYXvCdtrjA8=";
  };

  cmakeFlags = [
    "-DRIME_DATA_DIR=${placeholder "out"}/share/rime-data"
  ];

  nativeBuildInputs = [
    cmake
    kdePackages.extra-cmake-modules
    pkg-config
    gettext
    zstd
  ];

  buildInputs = [
    fcitx5
    librime
  ];

  postInstall = ''
    mkdir -p $out/share/rime-data
    cp -pRL "${combinedRimeData}/share/rime-data/." $out/share/rime-data/
  '';

  meta = {
    description = "RIME support for Fcitx5 with Rime-Ice configuration";
    homepage = "https://github.com/iDvel/rime-ice";
    platforms = lib.platforms.linux;
  };
}
